*BASIC
NEW
AUTO
REM>Receive
REM Receive a key pressed by a remote station
:
OSWORD=&FFF1:OSBYTE=&FFF4
max_rx_length%=20
DIM cblock% 13,rxbuffer% max_rx_length%
port%=100
:
station%=21
INPUT "From station: " S%
REPEAT
PROCRECEIVE(S%,port%)
key%=?rxbuffer%
PRINT "<";CHR$(key%);">";
UNTIL FALSE
:
DEFPROCRECEIVE(station%,port%)
:
REM First, set up the control block
?cblock%=0
cblock%?1=&7F
cblock%?2=port%
cblock%!3=station%
cblock%!5=rxbuffer%
cblock%!9=rxbuffer%+max_rx_length%
:
REM Now call OSWORD to open the RECEIVE block
X%=cblock%:Y%=cblock% DIV 256:A%=&11
CALL OSWORD
rxcb_number%=?cblock%
:
REM Now wait for the reception
A%=&33:X%=rxcb_number%
REPEAT:U%=USR OSBYTE
UNTIL (U% AND &8000)<>0
:
REM A reception has happened, so read the control block back
X%=cblock%:Y%=cblock% DIV 256:A%=&11
?cblock%=rxcb_number%:CALL OSWORD
bytes_received%=cblock%!9-cblock%!5
ENDPROC
