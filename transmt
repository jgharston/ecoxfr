*BASIC
NEW
AUTO
REM>Transmit
REM Transmit to keys to remote station
:
OSWORD=&FFF1:OSBYTE=&FFF4
max_tx_length%=20
txport%=100
DIM cblock% 13,txbuffer% max_tx_length%
INPUT "Remote station: " S%
PRINT "Now press keys..."
REPEAT
key%=GET
PRINT CHR$key%;
?txbuffer%=key%
tx_result%=FNTRANSMIT(S%,txport%,1)
UNTIL tx_result%<>0:REM Loop until a transmit failure
ON tx_result%-&3F GOTO 220,230,240,250,260
PRINT "Line jammed":END
PRINT "Net error":END
PRINT "Not listening":END
PRINT "No clock":END
PRINT "Bad TRANSMIT control block":END
DEFFNTRANSMIT(station%,port%,length%)
LOCAL X%,Y%,A%,tries%,delay%,U%,txresult%,nonfatal%
cblock%?1=port%
cblock%!2=station%
cblock%!4=txbuffer%
cblock%!8=txbuffer%+length%
tries%=10:delay%=50
REPEAT
:
REM Now attempt to start the transmission
X%=cblock%:Y%=cblock% DIV 256:A%=&10
REPEAT
?cblock%=&80
CALL OSWORD
UNTIL ?cblock%<>0
:
REM Now poll the TRANSMIT block until completion
REPEAT
A%=&32:U%=USR OSBYTE
UNTIL (U% AND &8000)=0
:
REM Now check if an error, and if a re-try is required
txresult%=(U% AND &FF00) DIV 256
nonfatal%=txresult%=&41 OR txresult%=&42
IF nonfatal% THEN PROCdelay(delay%):tries%=tries%-1
UNTIL tries%=0 OR NOT nonfatal%
REM Continue to loop until either a fatal error
REM or a successful transmission,
REM or all re-tries have failed
=txresult%
:
DEFPROCdelay(n%)
REM Wait for n% centiseconds
LOCAL limit%
limit%=TIME+n%
REPEAT
UNTIL TIME>limit%
ENDPROC
